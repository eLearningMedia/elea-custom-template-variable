apply plugin: "java"
apply plugin: "war"
apply plugin: "maven"
apply plugin: "eclipse"
apply plugin: "idea"

group "es.elearningmedia"
version "1.0-SNAPSHOT"

ext {
  deployServer = "192.168.0.101:9876"
  deployInstance = "BBLEARN"
}

repositories {
  mavenCentral()
  maven {
    url "https://maven.blackboard.com/content/repositories/releases/"
  }
}

configurations {
  buildUtils
}

// define the project's dependencies
dependencies {
  providedCompile "javax.servlet:servlet-api:2.5",
                  "javax.servlet.jsp:jsp-api:2.1"

  // Dependencies are libraries needed to build, but should not be included in the B2 WAR.
  // You should NEVER include Learn JARs (other than webapis) in your B2.
  providedCompile( "blackboard.platform:bb-platform:9.1.120113.0" ) { transitive = false }

  buildUtils "org.oscelot:b2deploy-task:0.1.0"
}

// Add a task to deploy a B2 using starting block
task deployB2( dependsOn: "war" ) << {
      ant.taskdef( name: "b2deploy", classname: "org.oscelot.ant.B2DeployTask", classpath: project.configurations.buildUtils.asPath )
      ant.b2deploy( localfilepath: project.war.archivePath, host: project.ext.deployServer, courseorgavailable: 'true', clean: 'true', webappName: 'bb-starting-block-' + project.ext.deployInstance )
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.6'
}
